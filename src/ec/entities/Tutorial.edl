module tutorial {

    D "This module contains entities for building a tutorial system."
    T "tutorial"

    entity Tutorial {
        D "Represents an entire tutorial with modules and sessions."
        primarykey uuid tutorialId

        attributes {
            creation date createdOn      { D "When the object was created." }
            modification date modifiedOn { D "When the object was last modified." }
            unique string identifier     { D "A unique identifier associated with this tutorial." }

            string title {
                D "The localized title of the tutorial."
            }
            optional string summary {
                D "The localized summary of the tutorial used when summarizing all tutorials in a view."
            }
            optional string overview {
                D "Tutorial overview."
            }
        }

        relationships {
            many Module modules {
                D "The modules of a tutorial."
            }
            one User createdUser {
                D "The user that created the tutorial."
            }
        }
    }

    entity Module {
        D "Represents a module within the tutorial. A module is a big partition of the tutorial."

        primarykey uuid moduleId

        attributes {
            creation date createdOn      { D "When the object was created." }
            modification date modifiedOn { D "When the object was last modified." }
            int32 number { D "Represents the module number." }

            string title {
                D "The localized title of the module."
            }
            optional string summary {
                D "The localized summary of the module used when sumarizing all modules of a tutorial."
            }
            optional string overview {
                D "Module overview."
            }
        }

        relationships {
            parent one Tutorial tutorial         { D "The tutorial to which this module belongs." }
            many Session sessions                { D "The sessions of this module." }
        }
    }

    entity Session {
        D "Represents a session within a module. A session typically tries to teach a single concept."
        primarykey uuid sessionId

        attributes {
            creation date createdOn      { D "When the object was created." }
            modification date modifiedOn { D "When the object was last modified." }
            string title {
                D "The title of the session."
            }
            optional string objective {
                D "The objective of the session."
            }
            optional string discussion {
                D "The discussion section of the session."
            }
            int32 number { D "The session number." }
        }

        relationships {
            parent one Module module               { D "The module to which this session belongs." }
            many Exercise exercises                { D "The exercises of this session." }
        }
    }

    entity Exercise {
        D "Represents an exercise within a session. A session typically only has one exercise but it can have"
        D "more than one if the session is big. An exercise gives the student some hands on experience with the"
        D "material covered by its session."

        primarykey uuid exerciseId

        attributes {
            creation date createdOn      { D "When the object was created." }
            modification date modifiedOn { D "When the object was last modified." }
            int32 number { D "The exercise number." }
            optional string overview {
                D "Exercise overview."
            }
        }

        relationships {
            parent one Session session           { D "The session to which this exercise belongs." }
            many Step steps                      { D "The steps of this exercise." }
        }
    }

    entity Step {
        D "An exercise is broken down into smaller steps where a single step has the user perform a small"
        D "task as part of the exercise."
        primarykey uuid stepId

        attributes {
            creation date createdOn      { D "When the object was created." }
            modification date modifiedOn { D "When the object was last modified." }
            int32 number { D "The step number." }
            optional string instructions {
                D "Step content."
            }
        }

        relationships {
            parent one Exercise exercise  { D "The exercise to which this step belongs." }
        }
    }
}